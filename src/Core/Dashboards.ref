*$FROM Core/DataSources
$EXTERN DataSource-Connect, DataSource-Finish, DataSource-Extended;

*$FROM Core/Widgets
$EXTERN Widget-Extended;

*$FROM Models/DashboardModel
$EXTERN Dashboard-Widgets, Dashboard-DataSource;

*$FROM Utils
$EXTERN TryMap;


$ENTRY Dashboard-Extended {
  t.Dashboard
    , <DataSource-Connect <Dashboard-DataSource t.Dashboard>>
    : {
      Success s.DataSourceConn
        , <Dashboard-Extended-ExtendWidgets s.DataSourceConn t.Dashboard>
        : {
          Success t.DashboardExtended
            , <DataSource-Finish s.DataSourceConn> :
            = Success t.DashboardExtended;

          Fails e.Errors
            , <DataSource-Finish s.DataSourceConn> :
            = Fails e.Errors;
        };

      Fails s.DataSourceConn e.Errors
        , <DataSource-Finish s.DataSourceConn> :
        = Fails e.Errors;
    }
}


Dashboard-Extended-ExtendWidgets {
  s.DataSourceConn t.Dashboard
    , <Dashboard-Widgets t.Dashboard> : (Array e.Widgets)
    , <TryMap (Widget-Extended s.DataSourceConn) e.Widgets>
    : {
      Success e.WidgetsExtended
        = <Dashboard-Extended-ExtendDataSource
          s.DataSourceConn
          <Dashboard-Widgets t.Dashboard (Array e.WidgetsExtended)>
        >;

      Fails e.Errors = Fails e.Errors;
    }
}


Dashboard-Extended-ExtendDataSource {
  s.DataSourceConn t.Dashboard
    , <DataSource-Extended s.DataSourceConn <Dashboard-DataSource t.Dashboard>>
    : {
      Success t.DataSourceExtended
        = Success <Dashboard-DataSource t.Dashboard t.DataSourceExtended>;

      Fails e.Errors = Fails e.Errors;
    }
}
