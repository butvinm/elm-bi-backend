*$FROM Server
$EXTERN HttpServer-Create, HttpServer-Run;

*$FROM Storage
$EXTERN LoadDashboards, DumpDashboards;

*$FROM Api/GetDashboardsHandler
$EXTERN GetDashboardsHandler;

*$FROM Api/GetDashboardHandler
$EXTERN GetDashboardHandler;

*$FROM Api/ApiUtils
$EXTERN ValidateJsonRequest, CreateErrorResponse;


$ENTRY Go {
  , <Prout 'HTTP server is running at http://localhost:6969'> :
  , <HttpServer-Create <LoadDashboards> HandleRequest> : t.Server
  = <HttpServer-Run t.Server 6969>;
}


HandleRequest {
  t.Ctx t.Method t.URI t.Headers e.Body
    , <ValidateJsonRequest t.Headers e.Body> : {
      Success t.Json
        , <Route t.Ctx t.Method t.URI t.Headers t.Json> : t.NewCtx e.Response
        , <DumpDashboards t.NewCtx> :
        = t.NewCtx e.Response;

      Fails e.Response = t.Ctx  e.Response;
    }
}


Route {
  t.Ctx ('POST') ('/get-dashboards') t.Headers t.Json
    = <GetDashboardsHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/get-dashboard') t.Headers t.Json
    = <GetDashboardHandler t.Ctx t.Headers t.Json>;

  t.Ctx (e.Method) (e.URI) t.Headers t.Json
    = t.Ctx
      <CreateErrorResponse 404 'Not Found :('>;
}
