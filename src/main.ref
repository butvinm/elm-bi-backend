*$FROM Server
$EXTERN HttpServer, HttpServer-Run;

*$FROM Storage
$EXTERN LoadDashboards, DumpDashboards;

*$FROM Api/GetDashboardsHandler
$EXTERN GetDashboardsHandler;

*$FROM Api/GetDashboardHandler
$EXTERN GetDashboardHandler;

*$FROM Api/DeleteDashboardHandler
$EXTERN DeleteDashboardHandler;

*$FROM Api/UpdateDashboardHandler
$EXTERN UpdateDashboardHandler;

*$FROM Api/CreateDashboardHandler
$EXTERN CreateDashboardHandler;

*$FROM Api/AddWidgetHandler
$EXTERN AddWidgetHandler;

*$FROM Api/DeleteWidgetHandler
$EXTERN DeleteWidgetHandler;

*$FROM Api/ApiUtils
$EXTERN ValidateJsonRequest, CreateOptionsResponse, CreateErrorResponse;


$ENTRY Go {
  , <Prout 'HTTP server is running at http://localhost:6969'> :
  , <LoadDashboards>
  : {
      Success t.Dashboards
        , <HttpServer t.Dashboards HandleRequest> : t.Server
        = <HttpServer-Run t.Server 6969>;

      Fails e.Error
        , <Prout e.Error> :
        = <Exit 1>;
    }
}


HandleRequest {
  t.Ctx ('OPTIONS') t.URI t.Headers e.Body
    = t.Ctx <CreateOptionsResponse>;

  t.Ctx ('POST') t.URI t.Headers e.Body
    , <ValidateJsonRequest t.Headers e.Body>
    : {
        Success t.Json
          , <HandleActionRequest t.Ctx t.URI t.Json> : t.NewCtx e.Response
          , <DumpDashboards t.NewCtx> :
          = t.NewCtx e.Response;

        Fails e.Response = t.Ctx e.Response;
      };

  t.Ctx t.Method t.URI t.Headers e.Body
    = t.Ctx <CreateErrorResponse 405>;
}


HandleActionRequest {
  t.Ctx ('/get-dashboards') t.Json
    = <GetDashboardsHandler t.Ctx t.Json>;

  t.Ctx ('/get-dashboard') t.Json
    = <GetDashboardHandler t.Ctx t.Json>;

  t.Ctx ('/create-dashboard') t.Json
    = <CreateDashboardHandler t.Ctx t.Json>;

  t.Ctx ('/update-dashboard') t.Json
    = <UpdateDashboardHandler t.Ctx t.Json>;

  t.Ctx ('/delete-dashboard') t.Json
    = <DeleteDashboardHandler t.Ctx t.Json>;

  t.Ctx ('/add-widget') t.Json
    = <AddWidgetHandler t.Ctx t.Json>;

  t.Ctx ('/delete-widget') t.Json
    = <DeleteWidgetHandler t.Ctx t.Json>;

  t.Ctx (e.URI) t.Json
    = t.Ctx <CreateErrorResponse 404 'Unknown action'>;
}
