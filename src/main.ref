*$FROM Server
$EXTERN HttpServer-Create, HttpServer-Run;

*$FROM Storage
$EXTERN LoadDashboards, DumpDashboards;

*$FROM Api/GetDashboardsHandler
$EXTERN GetDashboardsHandler;

*$FROM Api/GetDashboardHandler
$EXTERN GetDashboardHandler;

*$FROM Api/DeleteDashboardHandler
$EXTERN DeleteDashboardHandler;

*$FROM Api/UpdateDashboardHandler
$EXTERN UpdateDashboardHandler;

*$FROM Api/CreateDashboardHandler
$EXTERN CreateDashboardHandler;

*$FROM Api/AddWidgetHandler
$EXTERN AddWidgetHandler;

*$FROM Api/DeleteWidgetHandler
$EXTERN DeleteWidgetHandler;

*$FROM Api/ApiUtils
$EXTERN ValidateJsonRequest, CreateOptionsResponse, CreateErrorResponse;


$ENTRY Go {
  , <Prout 'HTTP server is running at http://localhost:6969'> :
  , <LoadDashboards>
  : {
      Success t.Dashboards
        , <HttpServer-Create t.Dashboards HandleRequest> : t.Server
        = <HttpServer-Run t.Server 6969>;

      Fails e.Error
        , <Prout e.Error> :
        = <Exit 1>;
    }
}


HandleRequest {
  t.Ctx ('OPTIONS') t.URI t.Headers e.Body
    = t.Ctx <CreateOptionsResponse>;

  t.Ctx t.Method t.URI t.Headers e.Body
    , <ValidateJsonRequest t.Headers e.Body>
    : {
        Success t.Json
          , <Route t.Ctx t.Method t.URI t.Headers t.Json> : t.NewCtx e.Response
          , <DumpDashboards t.NewCtx> :
          = t.NewCtx e.Response;

        Fails e.Response = t.Ctx  e.Response;
      }
}


Route {
  t.Ctx ('POST') ('/get-dashboards') t.Headers t.Json
    = <GetDashboardsHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/get-dashboard') t.Headers t.Json
    = <GetDashboardHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/create-dashboard') t.Headers t.Json
    = <CreateDashboardHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/update-dashboard') t.Headers t.Json
    = <UpdateDashboardHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/delete-dashboard') t.Headers t.Json
    = <DeleteDashboardHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/add-widget') t.Headers t.Json
    = <AddWidgetHandler t.Ctx t.Headers t.Json>;

  t.Ctx ('POST') ('/delete-widget') t.Headers t.Json
    = <DeleteWidgetHandler t.Ctx t.Headers t.Json>;

  t.Ctx (e.Method) (e.URI) t.Headers t.Json
    = t.Ctx
      <CreateErrorResponse 404 'Not Found :('>;
}
