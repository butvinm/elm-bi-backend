*$FROM LibraryEx
$EXTERN Apply;

*$FROM Socket
$EXTERN
  Socket-Open,
  Socket-Close,
  Socket-InitAddress,
  Socket-DeinitAddress,
  Socket-Bind,
  Socket-Listen,
  Socket-Accept,
  Socket-Send,
  Socket-Recv;

*$FROM Http
$EXTERN DecodeHttpRequest;


$ENTRY HttpServer {
  t.Ctx t.Handler = ((Ctx t.Ctx) (Handler t.Handler));
}


HttpServer-Ctx {
  (e.Server-B (Ctx t.Ctx) e.Server-E) = t.Ctx;
  (e.Server-B (Ctx t.Ctx) e.Server-E) t.NewCtx = (e.Server-B (Ctx t.NewCtx) e.Server-E);
}


HttpServer-Handler {
  (e.Server-B (Handler t.Handler) e.Server-E) = t.Handler;
  (e.Server-B (Handler t.Handler) e.Server-E) t.NewHandler = (e.Server-B (Handler t.NewHandler) e.Server-E);
}


AF_INET { = 2 }
SOCK_STREAM { = 1 }
INADDR_ANY { = 0 }

SOCKET_NO { = 69 }
ADDRESS_NO { = 42 }
MAX_CONNS { = 5 }


$ENTRY HttpServer-Run {
  t.Server s.Port
    = <Socket-Open <SOCKET_NO> <AF_INET> <SOCK_STREAM> 0>
      <Socket-InitAddress <ADDRESS_NO> <AF_INET> <INADDR_ANY> s.Port>
        <Socket-Bind <SOCKET_NO> <ADDRESS_NO>>
        <Socket-Listen <SOCKET_NO> <MAX_CONNS>>
        <AcceptLoop t.Server <SOCKET_NO> <ADDRESS_NO>>
      <Socket-DeinitAddress <ADDRESS_NO>>
      <Socket-Close <SOCKET_NO>>
}


AcceptLoop {
  t.Server s.SocketNo s.AddressNo
    , <Socket-Accept s.SocketNo s.AddressNo> : s.ClientSocketNo
    , <ProcessRequest t.Server s.ClientSocketNo> : t.NewServer
    = <AcceptLoop t.NewServer s.SocketNo s.AddressNo>
}


* TODO: optimize excessive e.Body and e.Response copies
ProcessRequest {
  t.Server s.ClientSocketNo
    , <HttpServer-Ctx t.Server> <HttpServer-Handler t.Server> : t.Ctx t.Handler
    , <DecodeHttpRequest <ReadRequest s.ClientSocketNo>>
    : {
        Success t.Method t.URI t.HttpVersion t.Headers e.Body
          , <Apply t.Handler t.Ctx t.Method t.URI t.Headers e.Body> : t.NewCtx e.Response
          , <Socket-Send s.ClientSocketNo e.Response> :
          , <Socket-Close s.ClientSocketNo> :
          = <HttpServer-Ctx t.Server t.NewCtx>;

        Fails e.Error
          , <Socket-Close s.ClientSocketNo> :
          = <HttpServer-Ctx t.Server t.Ctx>;
      }
}


ReadRequest {
  s.ClientSocketNo
    = <ReadRequest-ReadContent
      s.ClientSocketNo
      <ReadRequest-ReadHeaders s.ClientSocketNo>
    >
}


ReadRequest-ReadHeaders {
  s.ClientSocketNo
    , <Socket-Recv s.ClientSocketNo>
    : {
        0 = /* empty */ (/* empty */);

        s.BytesRead e.Headers '\r\n\r\n' e.Body = e.Headers '\r\n' (e.Body);

        s.BytesRead e.Headers = e.Headers <ReadRequest-ReadHeaders s.ClientSocketNo>;
      }
}


ReadRequest-ReadContent {
  s.ClientSocketNo e.Headers (e.Body)
    , e.Headers : e.Headers-B 'Content-Length: ' e.ContentLength '\r\n' e.Headers-E
    , <Lenw e.Body> : s.BodySize e.1
    = <ReadRequest-ReadContent-ByContentLength
        s.ClientSocketNo
        <Sub <Numb e.ContentLength> s.BodySize>
        e.Headers
        (e.Body)
      >;

  s.ClientSocketNo e.Headers (e.Body)
    = e.Headers '\r\n' e.Body;
}


ReadRequest-ReadContent-ByContentLength {
  s.ClientSocketNo 0 e.Headers (e.Body)
    = e.Headers '\r\n' e.Body;

* If for whatever reason we've read more than required
  s.ClientSocketNo '-' s.Remained e.Headers (e.Body)
    = e.Headers '\r\n' e.Body;

  s.ClientSocketNo s.Remained e.Headers (e.Body)
    , <Socket-Recv s.ClientSocketNo>
    : {
        0 = e.Headers '\r\n' e.Body;

        s.BytesRead e.Content
          = <ReadRequest-ReadContent-ByContentLength
              s.ClientSocketNo
              <Sub s.Remained s.BytesRead>
              e.Headers
              (e.Body e.Content)
            >;
      };
}
